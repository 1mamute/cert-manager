package(default_visibility = ["//visibility:public"])

exports_files(["00-crds.yaml"])

load("@io_k8s_repo_infra//defs:pkg.bzl", "pkg_tar")
load("//build:helm.bzl", "helm_tmpl")
load("//build:licensing.bzl", "licensed_file")

RELEASE_NAME = "cert-manager"

RELEASE_NAMESPACE = "cert-manager"

VARIANTS = {
    "cert-manager": {
        "crd_variant": "regular",
        "additional_api_versions": "",
        "values": {
            "installCRDs": "false",
        },
    },
    "cert-manager-legacy": {
        "crd_variant": "legacy",
        # This following line tells Helm that we need to use v1beta1 webhooks
        # When using helm template the API versions are determined for the version
        # helm was compiled with, there is no way to remove any from this list
        # the following hack adds an API version which we then can use to force
        # the template engine to use v1beta1 webhooks.
        "additional_api_versions": "hacking-helm.i-wish-this-wasnt-required.cert-manager.io/force-v1beta1-webhooks",
        "values": {
            "installCRDs": "false",
        },
    },
}

[genrule(
    name = "%s.nohelm.crds.yaml" % name,
    srcs = [
        "//deploy/crds:crds.%s" % meta["crd_variant"],
    ],
    outs = ["%s.crds.nohelm.out" % name],
    cmd = " ".join([
        "$(location //hack/filter-crd)",
        "-variant=no-helm",
        "$(location //deploy/crds:crds.%s)" % meta["crd_variant"],
        "> $@",
    ]),
    tools = [
        "//hack/filter-crd",
    ],
    visibility = ["//visibility:public"],
) for (name, meta) in VARIANTS.items()]

[helm_tmpl(
    name = "%s.manifests" % name,
    additional_api_versions = meta["additional_api_versions"],
    helm_pkg = "//deploy/charts/cert-manager",
    release_name = RELEASE_NAME,
    release_namespace = RELEASE_NAMESPACE,
    values = meta["values"],
) for (name, meta) in VARIANTS.items()]

[licensed_file(
    name = "%s.crds.yaml" % name,
    src = ":%s.nohelm.crds.yaml" % name,
) for (name, meta) in VARIANTS.items()]

[genrule(
    name = "%s.unlicensed" % name,
    srcs = [
        "//deploy/crds:crds.%s" % meta["crd_variant"],
        "01-namespace.yaml",
        "%s.manifests" % name,
    ],
    outs = ["%s.unlicensed.yaml" % name],
    cmd = " ".join([
        "cat",
        "$(location //deploy/crds:crds.%s)" % meta["crd_variant"],
        "$(location 01-namespace.yaml)",
        "$(location %s.manifests)" % name,
        "> $@",
    ]),
) for (name, meta) in VARIANTS.items()]

[genrule(
    name = "%s.unlicensed.nohelm" % name,
    srcs = [
        ":%s.unlicensed" % name,
    ],
    outs = ["%s.unlicensed.nohelm.yaml" % name],
    cmd = " ".join([
        "$(location //hack/filter-crd)",
        "-variant=no-helm",
        "$(location :%s.unlicensed)" % name,
        "> $@",
    ]),
    tools = [
        "//hack/filter-crd",
    ],
    visibility = ["//visibility:public"],
) for (name, meta) in VARIANTS.items()]

[licensed_file(
    name = "%s.yaml" % name,
    src = "%s.unlicensed.nohelm" % name,
) for (name, meta) in VARIANTS.items()]

pkg_tar(
    name = "manifests",
    srcs = [":%s.yaml" % name for name in VARIANTS.keys()] +
           [":%s.crds.yaml" % name for name in VARIANTS.keys()],
    extension = "tar.gz",
    mode = "0644",
    package_dir = "manifests",
    strip_prefix = ".",
)

filegroup(
    name = "package-srcs",
    srcs = glob(["**"]),
    tags = ["automanaged"],
    visibility = ["//visibility:private"],
)

filegroup(
    name = "all-srcs",
    srcs = [":package-srcs"],
    tags = ["automanaged"],
    visibility = ["//visibility:public"],
)
